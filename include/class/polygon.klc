PROGRAM class_name
%NOBUSYLAMP
%COMMENT = 'polygon object'
%NOLOCKGROUP

--*** ..todo:: Does not work for polygons with holes

%include namespace.m

%include errors.klt
%include systemvars.klt
%include systemlib.klt
%include paths.c.klt
%include paths.t.klt
%include draw.klt

VAR
  this : PATH nodedata = t_VERTEX
  sub_poly : PATH nodedata = t_VERTEX
  hull : PATH nodedata = t_VERTEX

  lines : PATH nodedata = t_SEG2D_POLY
  contours : PATH nodedata = t_VEC_PATH
  fl : FILE
  canvas : XYZWPR
  params : t_RASTER

%from systemlib.klh %import VEC2D
%from math.klh %import arclength, arcangle
%include csv.klh
%include errors.klh
%include draw.klh

%include polygon.klh
%include polygon.private.klh


-- ** private **

ROUTINE clear_hull
  VAR
    i : INTEGER
    status : INTEGER
  BEGIN
    i = PATH_LEN(hull)
    WHILE PATH_LEN(hull) > 0 DO
		  DELETE_NODE(hull,i,status)
      CHK_STAT(status)
      i = i - 1
	  ENDWHILE
  END clear_hull

ROUTINE clear_poly
  VAR
    i : INTEGER
    status : INTEGER
  BEGIN
    i = PATH_LEN(p)
    WHILE PATH_LEN(p) > 0 DO
		  DELETE_NODE(p,i,status)
      CHK_STAT(status)
      i = i - 1
	  ENDWHILE
  END clear_poly

ROUTINE clear_lines
  VAR
    i : INTEGER
    status : INTEGER
  BEGIN
    i = PATH_LEN(lines)
    WHILE PATH_LEN(lines) > 0 DO
		  DELETE_NODE(lines,i,status)
      CHK_STAT(status)
      i = i - 1
	  ENDWHILE
  END clear_lines

ROUTINE clear_contours
  VAR
    i : INTEGER
    status : INTEGER
  BEGIN
    i = PATH_LEN(contours)
    WHILE PATH_LEN(contours) > 0 DO
		  DELETE_NODE(contours,i,status)
      CHK_STAT(status)
      i = i - 1
	  ENDWHILE
  END clear_contours

ROUTINE new_sub_poly
  VAR
    nde_strt, nde_end : INTEGER
    i : INTEGER
    status : INTEGER
  BEGIN
    nde_strt = 1; nde_end = PATH_LEN(this);
    --find polygon
    IF (polygon > 0) THEN
      i = 1
      REPEAT
        nde_strt = i
        --handle uninitialized nextPoly
        IF UNINIT(this[i].nextPoly) THEN
          nde_end = PATH_LEN(this)
        ELSE
          nde_end = this[i].nextPoly - 1
        ENDIF

        i = this[i].nextPoly
      UNTIL(this[i].polygon > polygon)
    ENDIF

    --clear to sub path
    clear_poly(sub_poly)
    COPY_PATH(this, nde_strt, nde_end, sub_poly, status)
    CHK_STAT(status)

  END new_sub_poly

ROUTINE update_last_poly
  VAR
    i : INTEGER
    polygon : INTEGER
  BEGIN
    --append pointer to next polygon if other
    --polygons exist
    i = PATH_LEN(this)
    IF i > 0 THEN
      IF NOT UNINIT(this[i].polygon) THEN
        polygon = this[i].polygon + 1
      ELSE
        polygon = 1
      ENDIF
      
      WHILE (i > 0) DO
        IF UNINIT(this[i].nextPoly) THEN
          this[i].nextPoly = PATH_LEN(this) + 1
        ENDIF
        i = i - 1
      ENDWHILE
    ELSE
      polygon = 1
    ENDIF

    RETURN(polygon)
  END update_last_poly

-- ** public **

ROUTINE clear
  VAR
    i : INTEGER
    status : INTEGER
  BEGIN
    i = PATH_LEN(this)
    WHILE PATH_LEN(this) > 0 DO
		  DELETE_NODE(this,i,status)
      i = i - 1
	  ENDWHILE
    --uninitialize the canvas
    SET_UNINIT_F('class_name', 'canvas')

    --clear hull
    clear_hull
    --clear to sub path
    clear_poly(sub_poly)

    clear_lines
    clear_contours
  END clear

ROUTINE new
  BEGIN
    clear
    set_canvas((ZEROPOS(1)))
  END new

ROUTINE import_shape
  VAR
    header : STRING[16]
    b : BOOLEAN
    v : VECTOR
    vert : INTEGER
    poly : INTEGER
    status : INTEGER
  BEGIN
    csv__read_open(filename, fl)
    --read header
    header = csv__read_line(filename, fl)

    clear_poly(sub_poly)
    --vertex count
    vert = 0
    --intialize polygon count
    poly = 1
    REPEAT
      vert = vert + 1
      b = csv__read_vector(filename, fl, ',', v)
      IF b THEN
        IF TRUNC(v.z) > poly THEN
          append_polygon(sub_poly)
          clear_poly(sub_poly)

          --add first vertex of next polygon
          poly = TRUNC(v.z)
          APPEND_NODE(sub_poly,status)
          sub_poly[PATH_LEN(sub_poly)].coords = VEC2D(v.x, v.y)
          sub_poly[PATH_LEN(sub_poly)].polygon = poly
        ELSE
          --add vertex
          APPEND_NODE(sub_poly,status)
          sub_poly[PATH_LEN(sub_poly)].coords = VEC2D(v.x, v.y)
          sub_poly[PATH_LEN(sub_poly)].polygon = poly
        ENDIF
      ELSE
        --end of file append polygon
        append_polygon(sub_poly)
        clear_poly(sub_poly)
      ENDIF
    UNTIL(NOT b)

    csv__read_close(fl)

  END import_shape

ROUTINE set_canvas
  BEGIN
    canvas = frm
  END set_canvas

ROUTINE set_params
  BEGIN
    params.start_angle = start_angle
    params.dir = dir
    params.wall_lines = wall_lines
    params.line_width = line_width
    params.line_pitch = line_pitch
  END set_params

ROUTINE get_params
  BEGIN
    RETURN(params)
  END get_params

ROUTINE append_vertex
  VAR
    status : INTEGER
    nd : t_VERTEX
  BEGIN
    IF UNINIT(canvas) THEN
      karelError(VAR_UNINIT,'class_name member "canvas" needs to' + &
                ' be set before building polygons', ER_ABORT)
    ENDIF
    APPEND_NODE(this,status)
    CHK_STAT(status)
    nd.coords = VEC2D(vertex.x, vertex.y)
    nd.polygon = polygon
    this[PATH_LEN(this)] = nd
  END append_vertex

ROUTINE append_polygon
  VAR
    i : INTEGER
    polygon : INTEGER
    lastPoly : INTEGER
  BEGIN
    polygon = update_last_poly

    --retain last index to point to prevPoly
    IF PATH_LEN(this) > 0 THEN lastPoly = PATH_LEN(this); ENDIF

    FOR i=1 TO PATH_LEN(verticies) DO
      append_vertex(verticies[i].coords, (polygon))
      IF NOT UNINIT(lastPoly) AND (lastPoly > 0) THEN
        this[PATH_LEN(this)].prevPoly = lastPoly
      ENDIF
    ENDFOR
  END append_polygon

ROUTINE append_polygon_array
  VAR
    i : INTEGER
    polygon : INTEGER
    lastPoly : INTEGER
  BEGIN
    polygon = update_last_poly

    --retain last index to point to prevPoly
    IF PATH_LEN(this) > 0 THEN lastPoly = PATH_LEN(this); ENDIF

    FOR i=1 TO ARRAY_LEN(verticies) DO
      append_vertex(verticies[i], (polygon))
      IF NOT UNINIT(lastPoly) THEN
        this[PATH_LEN(this)].prevPoly = lastPoly
      ENDIF
    ENDFOR
  END append_polygon_array

ROUTINE get_poly
  VAR
    i, nd, status : INTEGER
  BEGIN
    IF polygon = 0 THEN
      COPY_PATH(this, 1, PATH_LEN(this), out_poly, status)
      CHK_STAT(status)
    ELSE
      nd = 1
      REPEAT
        i = nd
        IF UNINIT(this[i].nextPoly) THEN
          nd = PATH_LEN(this)
        ELSE
          nd = this[i].nextPoly
        ENDIF
      UNTIL(this[i].polygon = polygon)

      COPY_PATH(this, 1, nd, out_poly, status)
      CHK_STAT(status)
    ENDIF
  END get_poly 

ROUTINE get_lines
  VAR
    status : INTEGER
  BEGIN
    COPY_PATH(lines, start_i, end_i, out_lines, status)
    CHK_STAT(status)
  END get_lines

ROUTINE get_contours
  VAR
    status : INTEGER
  BEGIN
    COPY_PATH(contours, start_i, end_i, out_cntrs, status)
    CHK_STAT(status)
  END get_contours

ROUTINE get_bounding_box
  VAR
    bbox : t_RECT
  BEGIN
    bbox = draw__bounding_box(this, (params.start_angle))
    --convert to world coordinates
    RETURN(bbox)
  END get_bounding_box

ROUTINE trace
  VAR
    i : INTEGER
  BEGIN
    --use full polygon if sub_poly is
    --not defined
    IF PATH_LEN(sub_poly) = 0 THEN
      new_sub_poly(0)
    ENDIF

    --clear previous contours
    clear_contours

    --get walls
    i = 0
    WHILE (i < params.wall_lines) DO
      --clear inset
      clear_poly(sub_poly)
      --inset by width of line to fill
      --within boundaries
      draw__inset_polygon(this, (params.line_width/2 + i*params.line_pitch), hull, sub_poly)
      --trace polygon
      draw__trace(sub_poly, params.start_angle, (params.dir), line_type, contours)
      i = i + 1
    ENDWHILE

  END trace

ROUTINE raster
  VAR
    i, status : INTEGER
  BEGIN
    clear_lines
    --trace contours if wall_lines > 0
    trace(line_type)
    --raster the interior
    draw__raster_lines(sub_poly, hull, params.start_angle, params.line_width, params.line_pitch, (params.dir), lines)

  END raster

ROUTINE lines_to_vec_path
  VAR
    i : INTEGER
    status : INTEGER
    prev_vert, curr_vert, next_vert : INTEGER
  BEGIN
    IF UNINIT(canvas) THEN
      karelError(VAR_UNINIT,'class_name member "canvas" needs to' + &
                ' be set before converting', ER_ABORT)
    ENDIF

    --convert lines
    FOR i=1 TO PATH_LEN(lines) DO
      --r0
      APPEND_NODE(out_lines, status)
      out_lines[PATH_LEN(out_lines)].v = canvas:lines[i].r0
      out_lines[PATH_LEN(out_lines)].code = PTH_MOVETO
      out_lines[PATH_LEN(out_lines)].polygon = lines[i].polygon

      --r1
      APPEND_NODE(out_lines, status)
      out_lines[PATH_LEN(out_lines)].v = canvas:lines[i].r1
      out_lines[PATH_LEN(out_lines)].code = PTH_CLOSE
      out_lines[PATH_LEN(out_lines)].polygon = lines[i].polygon
    ENDFOR
  END lines_to_vec_path

ROUTINE contours_to_vec_path
  VAR
    i, status : INTEGER
    next_vert,prev_vert : INTEGER
  BEGIN
    IF UNINIT(canvas) THEN
      karelError(VAR_UNINIT,'class_name member "canvas" needs to' + &
                ' be set before converting', ER_ABORT)
    ENDIF

    --convert contours
    FOR i=1 TO PATH_LEN(contours) DO
      APPEND_NODE(out_cntrs, status)
      out_cntrs[PATH_LEN(out_cntrs)].v = canvas:contours[i].v
      out_cntrs[PATH_LEN(out_cntrs)].code = contours[i].code
      out_cntrs[PATH_LEN(out_cntrs)].polygon = contours[i].polygon
    ENDFOR

  END contours_to_vec_path

ROUTINE hexagon
  VAR
    cntr_pnt : XYZWPR
  BEGIN
    IF UNINIT(canvas) THEN
      karelError(VAR_UNINIT,'class_name member "canvas" needs to' + &
                ' be set before building polygons', ER_ABORT)
    ENDIF

    cntr_pnt = POS(translation.x,translation.y,translation.z,0,0,rotation,(ZEROPOS(1).Config_data))
    -- draw hexagon
    draw__hexagon(radius, verticies, cntr_pnt, this)

  END hexagon

ROUTINE pads
  VAR
    i : INTEGER
    poly : ARRAY[4] OF VECTOR
  BEGIN
    FOR i=0 TO no_of_pads-1 DO
      poly[1] = start + i*VEC2D(seperation, 0)
      poly[2] = start + i*VEC2D(seperation, 0) + VEC2D(width,0)
      poly[3] = start + i*VEC2D(seperation, 0) + VEC2D(width,length)
      poly[4] = start + i*VEC2D(seperation, 0) + VEC2D(0,length)
      append_polygon_array(poly)
    ENDFOR
  END pads

ROUTINE padsDeg
  VAR
    i : INTEGER
    poly : ARRAY[4] OF VECTOR
    start : VECTOR
    seperation : REAL
    width : REAL
  BEGIN
    canvas.z = canvas.z + radius

    seperation = math__arclength(seperatn_deg, radius)
    width = math__arclength(width_deg, radius)
    start.x = math__arclength(start_deg.x, radius) ; start.y = start_deg.y ; start.z = start_deg.z ;
    FOR i=0 TO no_of_pads-1 DO
      poly[1] = start + i*VEC2D(seperation, 0)
      poly[2] = start + i*VEC2D(seperation, 0) + VEC2D(width,0)
      poly[3] = start + i*VEC2D(seperation, 0) + VEC2D(width,length)
      poly[4] = start + i*VEC2D(seperation, 0) + VEC2D(0,length)
      append_polygon_array(poly)
    ENDFOR
  END padsDeg


  BEGIN
  END class_name
