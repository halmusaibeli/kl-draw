PROGRAM test_draw
%NOLOCKGROUP

%include systemvars.klt
%include draw.klt

VAR
  polygon1 : PATH nodedata = t_VERTEX
  polygon2 : PATH nodedata = t_VERTEX
  polygon3 : PATH nodedata = t_VERTEX
  polygon4 : PATH nodedata = t_VERTEX
  poly_inset : PATH nodedata = t_VERTEX
  hull : PATH nodedata = t_VERTEX
  lines : PATH nodedata = t_SEGMENT2D
  world_lines : PATH nodedata = t_SEGMENT

%include kunit.klh
%include draw.klh
%include draw.private.klh

%class tstpoly('polygon.klc', 'polygon.klh')


ROUTINE clear_path(p : PATH nodedata = t_VERTEX)
  VAR
    i, status : INTEGER
  BEGIN
    i = PATH_LEN(p)
    WHILE PATH_LEN(p) > 0 DO
		  DELETE_NODE(p,i,status)
      i = i - 1
	  ENDWHILE
  END clear_path

ROUTINE clear_lines(p : PATH nodedata = t_SEGMENT2D)
  VAR
    i, status : INTEGER
  BEGIN
    i = PATH_LEN(p)
    WHILE PATH_LEN(p) > 0 DO
		  DELETE_NODE(p,i,status)
      i = i - 1
	  ENDWHILE
  END clear_lines

ROUTINE clear_line3d(p : PATH nodedata = t_SEGMENT)
  VAR
    i, status : INTEGER
  BEGIN
    i = PATH_LEN(p)
    WHILE PATH_LEN(p) > 0 DO
		  DELETE_NODE(p,i,status)
      i = i - 1
	  ENDWHILE
  END clear_line3d

ROUTINE append_nodes(p : PATH nodedata = t_VERTEX; nodes : INTEGER)
  VAR
    i, status : INTEGER
  BEGIN
    FOR i=1 TO nodes DO
      APPEND_NODE(p,status)
    ENDFOR
  END append_nodes

ROUTINE path_to_arr(p : PATH nodedata = t_VERTEX; out_arr : ARRAY[*] OF VECTOR)
  VAR
    i : INTEGER
  BEGIN
    FOR i=1 TO PATH_LEN(p) DO
      out_arr[i] = p[i].coords
    ENDFOR
  END path_to_arr

ROUTINE t_intersect : BOOLEAN
  VAR
    clipline : t_LINE2D
    clipseg : t_SEGMENT2D
    line1, line2, line3, line4 : t_LINE2D
    seg1, seg2, seg3, seg4 : t_SEGMENT2D
    alpha1, alpha2 : REAL
    intersects : ARRAY[4] OF BOOLEAN
    eval_alpha : ARRAY[4] OF BOOLEAN
  BEGIN
    clipline.point = VEC2D(-5,3)
    clipline.vec = VEC2D(10,-6)
    --eq of line p = p_0 + v*t
    clipseg.r0 = clipline.point + clipline.vec*0
    clipseg.r1 = clipline.point + clipline.vec*1

    --intersects at (0,0)
    line1.point = VEC2D(-10,-4)
    line1.vec = VEC2D(20,8)
    --convert into segment
    seg1.r0 = line1.point + line1.vec*0
    seg1.r1 = line1.point + line1.vec*1

    intersects[1] = draw__intersect(clipseg, seg1, alpha1, alpha2)
    eval_alpha[1] = kunit_eq_r(0.5, alpha1) AND kunit_eq_r(0.5, alpha2)

    --does not intersect
    line2.point = VEC2D(-5,-4)
    line2.vec = VEC2D(10,0)
    --convert into segment
    seg2.r0 = line2.point + line2.vec*0
    seg2.r1 = line2.point + line2.vec*1

    intersects[2] = draw__intersect(clipseg, seg2, alpha1, alpha2)

    --intersects at endpoints
    line3.point = VEC2D(-5,-5)
    line3.vec = VEC2D(10,2)
    --convert into segment
    seg3.r0 = line3.point + line3.vec*0
    seg3.r1 = line3.point + line3.vec*1

    intersects[3] = draw__intersect(clipseg, seg3, alpha1, alpha2)
    eval_alpha[3] = kunit_eq_r(1, alpha1) AND kunit_eq_r(1, alpha2)

    --intersects at 0.82
    line4.point = VEC2D(-5,-5)
    line4.vec = VEC2D(3,8)
    --convert into segment
    seg4.r0 = line4.point + line4.vec*0
    seg4.r1 = line4.point + line4.vec*1

    intersects[4] = draw__intersect(clipseg, seg4, alpha1, alpha2)
    eval_alpha[4] = kunit_eq_r(0.244, alpha1) AND kunit_eq_r(0.816, alpha2)


    RETURN(kunit_assert(intersects[1]) AND eval_alpha[1] AND &
           kunit_assert(intersects[3]) AND eval_alpha[3] AND &
           kunit_assert(intersects[4]) AND eval_alpha[4] AND &
           NOT kunit_assert(intersects[2]) )
  END t_intersect

ROUTINE t_anchr_pnts : BOOLEAN
  VAR
    i : INTEGER
    act_min, act_max : INTEGER
    exp_min, exp_max : INTEGER
    act_min2, act_max2 : INTEGER
    exp_min2, exp_max2 : INTEGER
  BEGIN
    --set polygon
    clear_path(polygon1)
    append_nodes(polygon1, 10)
    polygon1[1].coords = VEC2D(0,6)
    polygon1[2].coords = VEC2D(1,4)
    polygon1[3].coords = VEC2D(3,4)
    polygon1[4].coords = VEC2D(1,3)
    polygon1[5].coords = VEC2D(2,0)
    polygon1[6].coords = VEC2D(0,2)
    polygon1[7].coords = VEC2D(-2,0)
    polygon1[8].coords = VEC2D(-1,3)
    polygon1[9].coords = VEC2D(-1,4)
    polygon1[10].coords = VEC2D(-3,4)

    act_min = draw__find_min(polygon1, VEC2D(0,1))
    act_max = draw__find_max(polygon1, VEC2D(0,1))

    exp_min = 7 ; exp_max = 1

    --set polygon
    clear_path(polygon2)
    append_nodes(polygon2, 6)
    polygon2[1].coords = VEC2D(3,0)
    polygon2[2].coords = VEC2D(5,4)
    polygon2[3].coords = VEC2D(9,4)
    polygon2[4].coords = VEC2D(9,-2)
    polygon2[5].coords = VEC2D(6,-3)
    polygon2[6].coords = VEC2D(6,0)

    act_min2 = draw__find_min(polygon2, VEC2D(0,1))
    act_max2 = draw__find_max(polygon2, VEC2D(0,1))

    exp_min2 = 5 ; exp_max2 = 3

    RETURN(kunit_eq_int(exp_min, act_min) AND kunit_eq_int(exp_max, act_max) AND &
           kunit_eq_int(exp_min2, act_min2) AND kunit_eq_int(exp_max2, act_max2))

  END t_anchr_pnts

ROUTINE t_convhull : BOOLEAN
  VAR
    exp_hull : ARRAY[6] OF VECTOR
    hull_arr : ARRAY[10] OF VECTOR
  BEGIN
    --set polygon
    clear_path(polygon1)
    append_nodes(polygon1, 10)
    polygon1[1].coords = VEC2D(0,6)
    polygon1[2].coords = VEC2D(1,4)
    polygon1[3].coords = VEC2D(3,4)
    polygon1[4].coords = VEC2D(1,3)
    polygon1[5].coords = VEC2D(2,0)
    polygon1[6].coords = VEC2D(0,2)
    polygon1[7].coords = VEC2D(-2,0)
    polygon1[8].coords = VEC2D(-1,3)
    polygon1[9].coords = VEC2D(-1,4)
    polygon1[10].coords = VEC2D(-3,4)

    --compute convex hull
    draw__convex_hull(polygon1, TRUE, hull)

    path_to_arr(hull, hull_arr)

    --expected hull
    exp_hull[1] = VEC2D(-2,0)
    exp_hull[2] = VEC2D(2,0)
    exp_hull[3] = VEC2D(3,4)
    exp_hull[4] = VEC2D(0,6)
    exp_hull[5] = VEC2D(-3,4)
    exp_hull[6] = VEC2D(-2,0)

    RETURN(kunit_eq_vec(exp_hull[1], hull_arr[1]) AND kunit_eq_vec(exp_hull[3], hull_arr[3]) AND &
           kunit_eq_vec(exp_hull[4], hull_arr[4]) AND kunit_eq_vec(exp_hull[6], hull_arr[6])  )

  END t_convhull

ROUTINE t_convhull_m : BOOLEAN
  VAR
    exp_hull : ARRAY[6] OF VECTOR
    hull_arr : ARRAY[10] OF VECTOR
  BEGIN
    --set polygon multiple bodies
    clear_path(polygon2)
    append_nodes(polygon2, 12)
    --square 1
    polygon2[1].coords = VEC2D(-8,3)
    polygon2[2].coords = VEC2D(-6,1.5)
    polygon2[3].coords = VEC2D(-3,5.5)
    polygon2[4].coords = VEC2D(-5,7)
    --square 2
    polygon2[5].coords = VEC2D(-4.17,0.13)
    polygon2[6].coords = VEC2D(0,-3)
    polygon2[7].coords = VEC2D(4.17,0.13)
    polygon2[8].coords = VEC2D(0,3.25)
    --square 3
    polygon2[9].coords = VEC2D(6,1.5)
    polygon2[10].coords = VEC2D(8,3)
    polygon2[11].coords = VEC2D(5,7)
    polygon2[12].coords = VEC2D(3,5.5)

    --compute convex hull
    draw__convex_hull(polygon2, TRUE, hull)

    path_to_arr(hull, hull_arr)

    --expected hull
    exp_hull[1] = VEC2D(0,-3)
    exp_hull[2] = VEC2D(8,3)
    exp_hull[3] = VEC2D(5,7)
    exp_hull[4] = VEC2D(-5,7)
    exp_hull[5] = VEC2D(8,3)
    exp_hull[6] = VEC2D(0,-3)

    RETURN(kunit_eq_vec(exp_hull[1], hull_arr[1]) AND kunit_eq_vec(exp_hull[3], hull_arr[3]) AND &
           kunit_eq_vec(exp_hull[4], hull_arr[4]) AND kunit_eq_vec(exp_hull[6], hull_arr[6]) )

  END t_convhull_m


ROUTINE t_bbox : BOOLEAN
  VAR
    act_box1 , act_box2 : t_RECT
    exp_box1 , exp_box2 : t_RECT
  BEGIN
    --set polygon
    clear_path(polygon2)
    append_nodes(polygon2, 6)

    polygon2[1].coords = VEC2D(0.34,-1.38)
    polygon2[2].coords = VEC2D(3.24,-0.7)
    polygon2[3].coords = VEC2D(6.22,1.74)
    polygon2[4].coords = VEC2D(3.48,5.46)
    polygon2[5].coords = VEC2D(-1.02,3.3)
    polygon2[6].coords = VEC2D(-1.74,0.84)
    
    act_box1 = draw__bounding_box(polygon2, 0)

    exp_box1.verts[1] = VEC2D(-1.74,-1.38)
    exp_box1.verts[2] = VEC2D(6.22,-1.38)
    exp_box1.verts[3] = VEC2D(6.22,5.46)
    exp_box1.verts[4] = VEC2D(-1.74,5.46)

    exp_box1.center = VEC2D(2.24,2.04)

    act_box2 = draw__bounding_box(polygon2, -45)

    exp_box2.verts[1] = VEC2D(-2.68,1.64)
    exp_box2.verts[2] = VEC2D(1.72,-2.76)
    exp_box2.verts[3] = VEC2D(6.71,2.23)
    exp_box2.verts[4] = VEC2D(2.31,6.63)

    exp_box2.center = VEC2D(2.015,1.935)

    RETURN(kunit_eq_vec(exp_box1.verts[1], act_box1.verts[1]) AND kunit_eq_vec(exp_box1.verts[4], act_box1.verts[4]) AND &
           kunit_eq_vec(exp_box1.center, act_box1.center) AND &
           kunit_eq_vec(exp_box2.verts[1], act_box2.verts[1]) AND kunit_eq_vec(exp_box2.verts[4], act_box2.verts[4]) AND &
           kunit_eq_vec(exp_box2.center, act_box2.center) )
  END t_bbox

ROUTINE t_p_on_poly : BOOLEAN
  VAR
    i : INTEGER
    v : ARRAY[4] OF VECTOR
    b : ARRAY[4] OF BOOLEAN
  BEGIN
    --set polygon
    clear_path(polygon1)
    append_nodes(polygon1, 10)
    polygon1[1].coords = VEC2D(0,6)
    polygon1[2].coords = VEC2D(-1,4)
    polygon1[3].coords = VEC2D(-3,4)
    polygon1[4].coords = VEC2D(-1,3)
    polygon1[5].coords = VEC2D(-2,0)
    polygon1[6].coords = VEC2D(0,2)
    polygon1[7].coords = VEC2D(2,0)
    polygon1[8].coords = VEC2D(1,3)
    polygon1[9].coords = VEC2D(3,4)
    polygon1[10].coords = VEC2D(1,4)

    --convex hull
    draw__convex_hull(polygon1, FALSE, hull)

    --set collision points
    v[1] = VEC2D(0,3.5)
    v[2] = VEC2D(-4,6)
    v[3] = VEC2D(-2,2)
    v[4] = VEC2D(-1.5,3.7)

    FOR i=1 TO ARRAY_LEN(v) DO
      b[i] = draw__point_collision_polygon(v[i], polygon1, hull)
    ENDFOR

    RETURN( kunit_assert(b[1]) AND NOT kunit_assert(b[2]) AND &
            NOT kunit_assert(b[3]) AND kunit_assert(b[4]) )


  END t_p_on_poly

ROUTINE t_p_on_poly2 : BOOLEAN
  VAR
    i : INTEGER
    v : ARRAY[4] OF VECTOR
    b : ARRAY[4] OF BOOLEAN
  BEGIN
    --set polygon
    clear_path(polygon2)
    append_nodes(polygon2, 10)
    polygon2[1].coords = VEC2D(6,0)
    polygon2[2].coords = VEC2D(10,0)
    polygon2[3].coords = VEC2D(10,1)
    polygon2[4].coords = VEC2D(8,1)
    polygon2[5].coords = VEC2D(8,3)
    polygon2[6].coords = VEC2D(9,3)
    polygon2[7].coords = VEC2D(9,2)
    polygon2[8].coords = VEC2D(10,2)
    polygon2[9].coords = VEC2D(10,4)
    polygon2[10].coords = VEC2D(6,4)

    --convex hull
    draw__convex_hull(polygon2, FALSE, hull)

    --set collision points
    v[1] = VEC2D(7,2)
    v[2] = VEC2D(8,4.5)
    v[3] = VEC2D(8.5,1.6)
    v[4] = VEC2D(9.6,2.4)

    FOR i=1 TO ARRAY_LEN(v) DO
      b[i] = draw__point_collision_polygon(v[i], polygon2, hull)
    ENDFOR

    RETURN( kunit_assert(b[1]) AND NOT kunit_assert(b[2]) AND &
            NOT kunit_assert(b[3]) AND kunit_assert(b[4]) )


  END t_p_on_poly2


ROUTINE t_clip_line : BOOLEAN
  VAR
    line : ARRAY[3] OF t_SEGMENT2D
    exp_line : ARRAY[4] OF t_SEGMENT2D
  BEGIN
    --clear lines 
    clear_lines(lines)

    --set polygon
    clear_path(polygon1)
    append_nodes(polygon1, 10)
    polygon1[1].coords = VEC2D(0,6)
    polygon1[2].coords = VEC2D(-1,4)
    polygon1[3].coords = VEC2D(-3,4)
    polygon1[4].coords = VEC2D(-1,3)
    polygon1[5].coords = VEC2D(-2,0)
    polygon1[6].coords = VEC2D(0,2)
    polygon1[7].coords = VEC2D(2,0)
    polygon1[8].coords = VEC2D(1,3)
    polygon1[9].coords = VEC2D(3,4)
    polygon1[10].coords = VEC2D(1,4)

    -- convex hull
    draw__convex_hull(polygon1, FALSE, hull)

    --create line
    line[1].r0 = VEC2D(-2,6)
    line[1].r1 = VEC2D(3,0)
    --clip line
    draw__clip_line_with_poly(line[1], polygon1, hull, lines)
    --expected lines
    exp_line[1].r0 = VEC2D(-0.75,4.5) ; exp_line[1].r1 = VEC2D(1.333,2) ;

    --create line
    line[2].r0 = VEC2D(-3,2)
    line[2].r1 = VEC2D(3,0.5)
    --clip line
    draw__clip_line_with_poly(line[2], polygon1, hull, lines)
    --expected lines
    exp_line[2].r0 = VEC2D(-1.462,1.615) ; exp_line[2].r1 = VEC2D(-0.6,1.4) ;
    exp_line[3].r0 = VEC2D(1,1) ; exp_line[3].r1 = VEC2D(1.727,0.818) ;
    
    --create line
    line[3].r0 = VEC2D(-2,5)
    line[3].r1 = VEC2D(2,5)
    --clip line
    draw__clip_line_with_poly(line[3], polygon1, hull, lines)

    exp_line[4].r0 = VEC2D(-0.5,5) ; exp_line[4].r1 = VEC2D(0.5,5) ;

    RETURN(kunit_eq_vec(exp_line[1].r0, lines[1].r0) AND kunit_eq_vec(exp_line[1].r1, lines[1].r1) AND &
    kunit_eq_vec(exp_line[2].r0, lines[2].r0) AND kunit_eq_vec(exp_line[2].r1, lines[2].r1) AND &
    kunit_eq_vec(exp_line[3].r0, lines[3].r0) AND kunit_eq_vec(exp_line[3].r1, lines[3].r1) AND &
    kunit_eq_vec(exp_line[4].r0, lines[4].r0) AND kunit_eq_vec(exp_line[4].r1, lines[4].r1) )

  END t_clip_line


ROUTINE t_clip_line2 : BOOLEAN
  VAR
    line : ARRAY[2] OF t_SEGMENT2D
    exp_line : ARRAY[5] OF t_SEGMENT2D
  BEGIN
    --clear lines 
    clear_lines(lines)

    --set polygon
    clear_path(polygon2)
    append_nodes(polygon2, 10)
    polygon2[1].coords = VEC2D(6,0)
    polygon2[2].coords = VEC2D(10,0)
    polygon2[3].coords = VEC2D(10,1)
    polygon2[4].coords = VEC2D(8,1)
    polygon2[5].coords = VEC2D(8,3)
    polygon2[6].coords = VEC2D(9,3)
    polygon2[7].coords = VEC2D(9,2)
    polygon2[8].coords = VEC2D(10,2)
    polygon2[9].coords = VEC2D(10,4)
    polygon2[10].coords = VEC2D(6,4)

    -- convex hull
    draw__convex_hull(polygon2, FALSE, hull)

    --create line
    line[1].r0 = VEC2D(7.32,0.37)
    line[1].r1 = VEC2D(9.66,2.41)
    --clip line
    draw__clip_line_with_poly(line[1], polygon2, hull, lines)

    --expected lines
    exp_line[1].r0 = VEC2D(7.32,0.37) ; exp_line[1].r1 = VEC2D(8.043,1)
    exp_line[2].r0 = VEC2D(9.190,2) ; exp_line[2].r1 = VEC2D(9.66,2.41)

    --create line
    line[2].r0 = VEC2D(8.44,3.59)
    line[2].r1 = VEC2D(10.09,0.5)
    --clip line
    draw__clip_line_with_poly(line[2], polygon2, hull, lines)

    --expected lines
    exp_line[3].r0 = VEC2D(8.44,3.590) ; exp_line[3].r1 = VEC2D(8.755,3) ;
    exp_line[4].r0 = VEC2D(9,2.541) ; exp_line[4].r1 = VEC2D(9.289,2) ;
    exp_line[5].r0 = VEC2D(9.823,1) ; exp_line[5].r1 = VEC2D(10,0.668) ;

    RETURN(kunit_eq_vec(exp_line[1].r0, lines[1].r0) AND kunit_eq_vec(exp_line[1].r1, lines[1].r1) AND &
           kunit_eq_vec(exp_line[2].r0, lines[2].r0) AND kunit_eq_vec(exp_line[2].r1, lines[2].r1) AND &
           kunit_eq_vec(exp_line[3].r0, lines[3].r0) AND kunit_eq_vec(exp_line[3].r1, lines[3].r1) AND &
           kunit_eq_vec(exp_line[4].r0, lines[4].r0) AND kunit_eq_vec(exp_line[4].r1, lines[4].r1) AND &
           kunit_eq_vec(exp_line[5].r0, lines[5].r0) AND kunit_eq_vec(exp_line[5].r1, lines[5].r1) )

  END t_clip_line2

ROUTINE t_clpln_mult : BOOLEAN
  VAR
    line : ARRAY[1] OF t_SEGMENT2D
    exp_line : ARRAY[3] OF t_SEGMENT2D
  BEGIN
    --clear lines 
    clear_lines(lines)

    --set polygon
    clear_path(polygon3)
    append_nodes(polygon3, 12)
    polygon3[1].coords = VEC2D(0,14) ; polygon3[1].polygon = 1 ; polygon3[1].nextPoly = 5 ;  
    polygon3[2].coords = VEC2D(0,10) ; polygon3[2].polygon = 1 ; polygon3[2].nextPoly = 5 ;  
    polygon3[3].coords = VEC2D(2,10) ; polygon3[3].polygon = 1 ; polygon3[3].nextPoly = 5 ;  
    polygon3[4].coords = VEC2D(2,14) ; polygon3[4].polygon = 1 ; polygon3[4].nextPoly = 5 ;  
    polygon3[5].coords = VEC2D(4,14) ; polygon3[5].polygon = 2 ; polygon3[5].nextPoly = 9 ;  
    polygon3[6].coords = VEC2D(4,10) ; polygon3[6].polygon = 2 ; polygon3[6].nextPoly = 9 ;  
    polygon3[7].coords = VEC2D(6,10) ; polygon3[7].polygon = 2 ; polygon3[7].nextPoly = 9 ;  
    polygon3[8].coords = VEC2D(6,14) ; polygon3[8].polygon = 2 ; polygon3[8].nextPoly = 9 ;  
    polygon3[9].coords = VEC2D(8,14) ; polygon3[9].polygon = 3 ;
    polygon3[10].coords = VEC2D(8,10) ; polygon3[10].polygon = 3 ;
    polygon3[11].coords = VEC2D(10,10) ; polygon3[11].polygon = 3 ;
    polygon3[12].coords = VEC2D(10,14) ; polygon3[12].polygon = 3 ;

    -- convex hull
    draw__convex_hull(polygon3, FALSE, hull)

    --create line
    line[1].r0 = VEC2D(-1.96,10.89)
    line[1].r1 = VEC2D(11.68,12.84)
    --clip line
    draw__clip_line_with_poly(line[1], polygon3, hull, lines)

    --expected lines
    exp_line[1].r0 = VEC2D(0,11.17) ; exp_line[1].r1 = VEC2D(2,11.456)
    exp_line[2].r0 = VEC2D(4,11.742) ; exp_line[2].r1 = VEC2D(6,12.028)
    exp_line[3].r0 = VEC2D(8,12.314) ; exp_line[3].r1 = VEC2D(10,12.6)

    RETURN(kunit_eq_vec(exp_line[1].r0, lines[1].r0) AND kunit_eq_vec(exp_line[1].r1, lines[1].r1) AND &
           kunit_eq_vec(exp_line[2].r0, lines[2].r0) AND kunit_eq_vec(exp_line[2].r1, lines[2].r1) AND &
           kunit_eq_vec(exp_line[3].r0, lines[3].r0) AND kunit_eq_vec(exp_line[3].r1, lines[3].r1) )

  END t_clpln_mult

ROUTINE t_raster1 : BOOLEAN
  VAR
    exp_line : ARRAY[6] OF t_SEGMENT2D
    b1, b2 : ARRAY[6] OF BOOLEAN
  BEGIN
    --clear lines 
    clear_lines(lines)

    --set polygon
    clear_path(polygon1)
    append_nodes(polygon1, 10)
    polygon1[1].coords = VEC2D(0,6)
    polygon1[2].coords = VEC2D(-1,4)
    polygon1[3].coords = VEC2D(-3,4)
    polygon1[4].coords = VEC2D(-1,3)
    polygon1[5].coords = VEC2D(-2,0)
    polygon1[6].coords = VEC2D(0,2)
    polygon1[7].coords = VEC2D(2,0)
    polygon1[8].coords = VEC2D(1,3)
    polygon1[9].coords = VEC2D(3,4)
    polygon1[10].coords = VEC2D(1,4)

    draw__raster_lines(polygon1, hull, 0, 1, 1, lines)

    --expected lines
    exp_line[1].r0 = VEC2D(-1.5,1.5) ; exp_line[1].r1 = VEC2D(-0.5,1.5)
    exp_line[2].r0 = VEC2D(-2,3.5) ; exp_line[2].r1 = VEC2D(2,3.5)
    exp_line[3].r0 = VEC2D(-0.25,5.5) ; exp_line[3].r1 = VEC2D(0.25,5.5)

    b1[1] = kunit_eq_vec(exp_line[1].r0, lines[3].r0)
    b1[2] = kunit_eq_vec(exp_line[1].r1, lines[3].r1)
    b1[3] = kunit_eq_vec(exp_line[2].r0, lines[6].r0)
    b1[4] = kunit_eq_vec(exp_line[2].r1, lines[6].r1)
    b1[5] = kunit_eq_vec(exp_line[3].r0, lines[8].r0)
    b1[6] = kunit_eq_vec(exp_line[3].r1, lines[8].r1)

    --clear lines 
    clear_lines(lines)

    draw__raster_lines(polygon1, hull, 45, 1, 1, lines)

    --expected lines
    exp_line[4].r0 = VEC2D(0.939,1.061) ; exp_line[4].r1 = VEC2D(1.47,1.591)
    exp_line[5].r0 = VEC2D(-2.185,3.593) ; exp_line[5].r1 = VEC2D(-1.778,4)
    exp_line[6].r0 = VEC2D(-0.221,5.556) ; exp_line[6].r1 = VEC2D(0.073,5.852)

    b2[1] = kunit_eq_vec(exp_line[4].r0, lines[2].r0)
    b2[2] = kunit_eq_vec(exp_line[4].r1, lines[2].r1)
    b2[3] = kunit_eq_vec(exp_line[5].r0, lines[7].r0)
    b2[4] = kunit_eq_vec(exp_line[5].r1, lines[7].r1)
    b2[5] = kunit_eq_vec(exp_line[6].r0, lines[8].r0)
    b2[6] = kunit_eq_vec(exp_line[6].r1, lines[8].r1)

    RETURN(kunit_eq_arb(b1) AND kunit_eq_arb(b2))

  END t_raster1

ROUTINE t_inset : BOOLEAN
  VAR
    exp_vert : ARRAY[8] OF VECTOR
  BEGIN
    --set polygon
    clear_path(polygon4)
    append_nodes(polygon4, 8)
    polygon4[1].coords = VEC2D(-8,0)
    polygon4[2].coords = VEC2D(-2,0)
    polygon4[3].coords = VEC2D(-2,2)
    polygon4[4].coords = VEC2D(-4,2)
    polygon4[5].coords = VEC2D(-4,8)
    polygon4[6].coords = VEC2D(-6,8)
    polygon4[7].coords = VEC2D(-6,2)
    polygon4[8].coords = VEC2D(-8,2)

    exp_vert[1] = VEC2D(-2.4,0.4)
    exp_vert[2] = VEC2D(-2.4,1.6)
    exp_vert[3] = VEC2D(-4.4,1.6)
    exp_vert[4] = VEC2D(-4.4,7.6)
    exp_vert[5] = VEC2D(-5.6,7.6)
    exp_vert[6] = VEC2D(-5.6,1.6)
    exp_vert[7] = VEC2D(-7.6,1.6)
    exp_vert[8] = VEC2D(-7.6,0.4)

    clear_path(poly_inset)
    draw__inset_polygon(polygon4, 0.4, hull, poly_inset)

    RETURN(kunit_eq_vec(exp_vert[1], poly_inset[1].coords) AND kunit_eq_vec(exp_vert[3], poly_inset[3].coords) AND &
           kunit_eq_vec(exp_vert[5], poly_inset[5].coords) AND kunit_eq_vec(exp_vert[6], poly_inset[6].coords) AND &
           kunit_eq_vec(exp_vert[8], poly_inset[8].coords) )
  END t_inset

ROUTINE t_inset_mult : BOOLEAN
  VAR
    exp_vert : ARRAY[12] OF VECTOR
  BEGIN
    --set polygon
    clear_path(polygon3)
    append_nodes(polygon3, 12)
    polygon3[1].coords = VEC2D(0,14) ; polygon3[1].polygon = 1 ; polygon3[1].nextPoly = 5 ;  
    polygon3[2].coords = VEC2D(0,10) ; polygon3[2].polygon = 1 ; polygon3[2].nextPoly = 5 ;  
    polygon3[3].coords = VEC2D(2,10) ; polygon3[3].polygon = 1 ; polygon3[3].nextPoly = 5 ;  
    polygon3[4].coords = VEC2D(2,14) ; polygon3[4].polygon = 1 ; polygon3[4].nextPoly = 5 ;  
    polygon3[5].coords = VEC2D(4,14) ; polygon3[5].polygon = 2 ; polygon3[5].nextPoly = 9 ;  
    polygon3[6].coords = VEC2D(4,10) ; polygon3[6].polygon = 2 ; polygon3[6].nextPoly = 9 ;  
    polygon3[7].coords = VEC2D(6,10) ; polygon3[7].polygon = 2 ; polygon3[7].nextPoly = 9 ;  
    polygon3[8].coords = VEC2D(6,14) ; polygon3[8].polygon = 2 ; polygon3[8].nextPoly = 9 ;  
    polygon3[9].coords = VEC2D(8,14) ; polygon3[9].polygon = 3 ;
    polygon3[10].coords = VEC2D(8,10) ; polygon3[10].polygon = 3 ;
    polygon3[11].coords = VEC2D(10,10) ; polygon3[11].polygon = 3 ;
    polygon3[12].coords = VEC2D(10,14) ; polygon3[12].polygon = 3 ;

    clear_path(poly_inset)
    draw__inset_polygon(polygon3, 0.5, hull, poly_inset)
    
    --expected verts
    exp_vert[1] = VEC2D(0.5,10.5)
    exp_vert[2] = VEC2D(1.5,10.5)
    exp_vert[3] = VEC2D(1.5,13.5)
    exp_vert[4] = VEC2D(0.5,13.5)
    exp_vert[5] = VEC2D(4.5,10.5)
    exp_vert[6] = VEC2D(5.5,10.5)
    exp_vert[7] = VEC2D(5.5,13.5)
    exp_vert[8] = VEC2D(4.5,13.5)
    exp_vert[9] = VEC2D(8.5,10.5)
    exp_vert[10] = VEC2D(9.5,10.5)
    exp_vert[11] = VEC2D(9.5,13.5)
    exp_vert[12] = VEC2D(8.5,13.5)


    RETURN(kunit_eq_vec(exp_vert[1], poly_inset[1].coords) AND kunit_eq_vec(exp_vert[4], poly_inset[4].coords) AND &
           kunit_eq_vec(exp_vert[6], poly_inset[6].coords) AND kunit_eq_vec(exp_vert[7], poly_inset[7].coords) AND &
           kunit_eq_vec(exp_vert[9], poly_inset[9].coords) AND kunit_eq_vec(exp_vert[12], poly_inset[12].coords) AND &
           kunit_eq_int((polygon3[10].polygon), (poly_inset[10].polygon)) AND kunit_eq_int((polygon3[7].polygon), (poly_inset[7].polygon)) AND &
           kunit_eq_int((polygon3[2].nextPoly), (poly_inset[2].nextPoly)) AND kunit_eq_int((polygon3[7].nextPoly), (poly_inset[7].nextPoly)) )
  END t_inset_mult

ROUTINE t_trace : BOOLEAN
  VAR
    exp_line : ARRAY[6] OF t_SEGMENT2D
  BEGIN
    --clear lines 
    clear_lines(lines)

    --set polygon
    clear_path(polygon1)
    append_nodes(polygon1, 10)
    polygon1[1].coords = VEC2D(0,6)
    polygon1[2].coords = VEC2D(-1,4)
    polygon1[3].coords = VEC2D(-3,4)
    polygon1[4].coords = VEC2D(-1,3)
    polygon1[5].coords = VEC2D(-2,0)
    polygon1[6].coords = VEC2D(0,2)
    polygon1[7].coords = VEC2D(2,0)
    polygon1[8].coords = VEC2D(1,3)
    polygon1[9].coords = VEC2D(3,4)
    polygon1[10].coords = VEC2D(1,4)

    --draw lines around polygon
    draw__trace(polygon1, 0, lines)

    --clear inset
    clear_path(poly_inset)
    --inset polygon
    draw__inset_polygon(polygon1, 0.5, hull, poly_inset)

    --draw lines around inset
    draw__trace(poly_inset, 45, lines)

    --expected lines
    exp_line[1].r0 = VEC2D(3,4) ; exp_line[1].r1 = VEC2D(1,4) --lines[5]
    exp_line[2].r0 = VEC2D(-1,4) ; exp_line[2].r1 = VEC2D(-3,4) --lines[8]
    exp_line[3].r0 = VEC2D(-1,3) ; exp_line[3].r1 = VEC2D(-2,0) --lines[10]
    exp_line[4].r0 = VEC2D(0.855,1.851) ; exp_line[4].r1 = VEC2D(0.388,3.253) --lines[11]
    exp_line[5].r0 = VEC2D(0,4.882) ; exp_line[5].r1 = VEC2D(-0.691,3.5) --lines[14]
    exp_line[6].r0 = VEC2D(0,2.707) ; exp_line[6].r1 = VEC2D(0.855,1.851) --lines[18]

    RETURN(kunit_eq_vec(exp_line[1].r0, lines[5].r0) AND kunit_eq_vec(exp_line[1].r1, lines[5].r1) AND &
           kunit_eq_vec(exp_line[2].r0, lines[8].r0) AND kunit_eq_vec(exp_line[2].r1, lines[8].r1) AND &
           kunit_eq_vec(exp_line[3].r0, lines[10].r0) AND kunit_eq_vec(exp_line[3].r1, lines[10].r1) AND &
           kunit_eq_vec(exp_line[4].r0, lines[11].r0) AND kunit_eq_vec(exp_line[4].r1, lines[11].r1) AND &
           kunit_eq_vec(exp_line[5].r0, lines[14].r0) AND kunit_eq_vec(exp_line[5].r1, lines[14].r1) AND &
           kunit_eq_vec(exp_line[6].r0, lines[18].r0) AND kunit_eq_vec(exp_line[6].r1, lines[18].r1) )

  END t_trace

ROUTINE t_class_poly : BOOLEAN
  VAR
    arr_poly : ARRAY[10] OF VECTOR
  BEGIN

    tstpoly__clear

    --set canvas
    tstpoly__set_canvas(POS(212.132,40,212.132,0,-45,0,(ZEROPOS(1).Config_data)))

    --draw polygon
    arr_poly[1] = VEC2D(6,0)
    arr_poly[2] = VEC2D(10,0)
    arr_poly[3] = VEC2D(10,1)
    arr_poly[4] = VEC2D(8,1)
    arr_poly[5] = VEC2D(8,3)
    arr_poly[6] = VEC2D(9,3)
    arr_poly[7] = VEC2D(9,2)
    arr_poly[8] = VEC2D(10,2)
    arr_poly[9] = VEC2D(10,4)
    arr_poly[10] = VEC2D(6,4)

    tstpoly__append_polygon(arr_poly)
    --raster lines
    tstpoly__raster(0.2, 0.2, 2, 45)

    --convert to world
    clear_line3d(world_lines)
    tstpoly__world_transform(world_lines)


    RETURN(TRUE)
  END t_class_poly

BEGIN
  total asserts 111
  kunit_test('test line intersection', t_intersect) -- asserts 10 
  kunit_test('test finding min and max points in polygon', t_anchr_pnts) -- asserts 4
  kunit_test('test convex hull star shape', t_convhull) -- asserts 4
  kunit_test('test convex hull of multiple polygons', t_convhull_m) -- asserts 4
  kunit_test('test bounding box', t_bbox) -- asserts 6
  kunit_test('test point collision on star', t_p_on_poly) -- asserts 4
  kunit_test('test point collision on c shaped', t_p_on_poly2) -- asserts 4
  kunit_test('line clip star polygon', t_clip_line) --asserts 8
  kunit_test('line clip on C shape', t_clip_line2) -- asserts 10
  kunit_test('multi polygon line clip', t_clpln_mult) -- asserts 6
  kunit_test('raster polygon', t_raster1) --asserts 24
  kunit_test('inset oblong T shape', t_inset) --asserts 5
  kunit_test('inset multiple polygons', t_inset_mult) --asserts 10
  kunit_test('trace lines around star polygon', t_trace) --asserts 12
  kunit_test('test polygon class', t_class_poly)
  kunit_done
END test_draw